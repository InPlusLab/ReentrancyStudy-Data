/**

 *Submitted for verification at Etherscan.io on 2018-09-25

*/



pragma solidity ^0.4.24;



// File: openzeppelin-solidity/contracts/math/SafeMath.sol



/**

 * @title SafeMath

 * @dev Math operations with safety checks that throw on error

 */

library SafeMath {



  /**

  * @dev Multiplies two numbers, throws on overflow.

  */

  function mul(uint256 _a, uint256 _b) internal pure returns (uint256 c) {

    // Gas optimization: this is cheaper than asserting 'a' not being zero, but the

    // benefit is lost if 'b' is also tested.

    // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522

    if (_a == 0) {

      return 0;

    }



    c = _a * _b;

    assert(c / _a == _b);

    return c;

  }



  /**

  * @dev Integer division of two numbers, truncating the quotient.

  */

  function div(uint256 _a, uint256 _b) internal pure returns (uint256) {

    // assert(_b > 0); // Solidity automatically throws when dividing by 0

    // uint256 c = _a / _b;

    // assert(_a == _b * c + _a % _b); // There is no case in which this doesn't hold

    return _a / _b;

  }



  /**

  * @dev Subtracts two numbers, throws on overflow (i.e. if subtrahend is greater than minuend).

  */

  function sub(uint256 _a, uint256 _b) internal pure returns (uint256) {

    assert(_b <= _a);

    return _a - _b;

  }



  /**

  * @dev Adds two numbers, throws on overflow.

  */

  function add(uint256 _a, uint256 _b) internal pure returns (uint256 c) {

    c = _a + _b;

    assert(c >= _a);

    return c;

  }

}



// File: openzeppelin-solidity/contracts/token/ERC20/ERC20Basic.sol



/**

 * @title ERC20Basic

 * @dev Simpler version of ERC20 interface

 * See https://github.com/ethereum/EIPs/issues/179

 */

contract ERC20Basic {

  function totalSupply() public view returns (uint256);

  function balanceOf(address _who) public view returns (uint256);

  function transfer(address _to, uint256 _value) public returns (bool);

  event Transfer(address indexed from, address indexed to, uint256 value);

}



// File: openzeppelin-solidity/contracts/token/ERC20/ERC20.sol



/**

 * @title ERC20 interface

 * @dev see https://github.com/ethereum/EIPs/issues/20

 */

contract ERC20 is ERC20Basic {

  function allowance(address _owner, address _spender)

    public view returns (uint256);



  function transferFrom(address _from, address _to, uint256 _value)

    public returns (bool);



  function approve(address _spender, uint256 _value) public returns (bool);

  event Approval(

    address indexed owner,

    address indexed spender,

    uint256 value

  );

}



// File: contracts/interface/IBasicMultiToken.sol



contract IBasicMultiToken is ERC20 {

    event Bundle(address indexed who, address indexed beneficiary, uint256 value);

    event Unbundle(address indexed who, address indexed beneficiary, uint256 value);



    ERC20[] public tokens;



    function tokensCount() public view returns(uint256);



    function bundleFirstTokens(address _beneficiary, uint256 _amount, uint256[] _tokenAmounts) public;

    function bundle(address _beneficiary, uint256 _amount) public;



    function unbundle(address _beneficiary, uint256 _value) public;

    function unbundleSome(address _beneficiary, uint256 _value, ERC20[] _tokens) public;



    function disableBundling() public;

    function enableBundling() public;

}



// File: contracts/interface/IMultiToken.sol



contract IMultiToken is IBasicMultiToken {

    event Update();

    event Change(address indexed _fromToken, address indexed _toToken, address indexed _changer, uint256 _amount, uint256 _return);



    mapping(address => uint256) public weights;



    function getReturn(address _fromToken, address _toToken, uint256 _amount) public view returns (uint256 returnAmount);

    function change(address _fromToken, address _toToken, uint256 _amount, uint256 _minReturn) public returns (uint256 returnAmount);



    function disableChanges() public;

}



// File: contracts/ext/CheckedERC20.sol



library CheckedERC20 {

    using SafeMath for uint;



    function isContract(address addr) internal view returns(bool result) {

        // solium-disable-next-line security/no-inline-assembly

        assembly {

            result := gt(extcodesize(addr), 0)

        }

    }



    function handleReturnBool() internal pure returns(bool result) {

        // solium-disable-next-line security/no-inline-assembly

        assembly {

            switch returndatasize()

            case 0 { // not a std erc20

                result := 1

            }

            case 32 { // std erc20

                returndatacopy(0, 0, 32)

                result := mload(0)

            }

            default { // anything else, should revert for safety

                revert(0, 0)

            }

        }

    }



    function handleReturnBytes32() internal pure returns(bytes32 result) {

        // solium-disable-next-line security/no-inline-assembly

        assembly {

            if eq(returndatasize(), 32) { // not a std erc20

                returndatacopy(0, 0, 32)

                result := mload(0)

            }

            if gt(returndatasize(), 32) { // std erc20

                returndatacopy(0, 64, 32)

                result := mload(0)

            }

            if lt(returndatasize(), 32) { // anything else, should revert for safety

                revert(0, 0)

            }

        }

    }



    function asmTransfer(address _token, address _to, uint256 _value) internal returns(bool) {

        require(isContract(_token));

        // solium-disable-next-line security/no-low-level-calls

        require(_token.call(bytes4(keccak256("transfer(address,uint256)")), _to, _value));

        return handleReturnBool();

    }



    function asmTransferFrom(address _token, address _from, address _to, uint256 _value) internal returns(bool) {

        require(isContract(_token));

        // solium-disable-next-line security/no-low-level-calls

        require(_token.call(bytes4(keccak256("transferFrom(address,address,uint256)")), _from, _to, _value));

        return handleReturnBool();

    }



    function asmApprove(address _token, address _spender, uint256 _value) internal returns(bool) {

        require(isContract(_token));

        // solium-disable-next-line security/no-low-level-calls

        require(_token.call(bytes4(keccak256("approve(address,uint256)")), _spender, _value));

        return handleReturnBool();

    }



    //



    function checkedTransfer(ERC20 _token, address _to, uint256 _value) internal {

        if (_value > 0) {

            uint256 balance = _token.balanceOf(this);

            asmTransfer(_token, _to, _value);

            require(_token.balanceOf(this) == balance.sub(_value), "checkedTransfer: Final balance didn't match");

        }

    }



    function checkedTransferFrom(ERC20 _token, address _from, address _to, uint256 _value) internal {

        if (_value > 0) {

            uint256 toBalance = _token.balanceOf(_to);

            asmTransferFrom(_token, _from, _to, _value);

            require(_token.balanceOf(_to) == toBalance.add(_value), "checkedTransfer: Final balance didn't match");

        }

    }



    //



    function asmName(address _token) internal view returns(bytes32) {

        require(isContract(_token));

        // solium-disable-next-line security/no-low-level-calls

        require(_token.call(bytes4(keccak256("name()"))));

        return handleReturnBytes32();

    }



    function asmSymbol(address _token) internal view returns(bytes32) {

        require(isContract(_token));

        // solium-disable-next-line security/no-low-level-calls

        require(_token.call(bytes4(keccak256("symbol()"))));

        return handleReturnBytes32();

    }

}



// File: openzeppelin-solidity/contracts/ownership/Ownable.sol



/**

 * @title Ownable

 * @dev The Ownable contract has an owner address, and provides basic authorization control

 * functions, this simplifies the implementation of "user permissions".

 */

contract Ownable {

  address public owner;





  event OwnershipRenounced(address indexed previousOwner);

  event OwnershipTransferred(

    address indexed previousOwner,

    address indexed newOwner

  );





  /**

   * @dev The Ownable constructor sets the original `owner` of the contract to the sender

   * account.

   */

  constructor() public {

    owner = msg.sender;

  }



  /**

   * @dev Throws if called by any account other than the owner.

   */

  modifier onlyOwner() {

    require(msg.sender == owner);

    _;

  }



  /**

   * @dev Allows the current owner to relinquish control of the contract.

   * @notice Renouncing to ownership will leave the contract without an owner.

   * It will not be possible to call the functions with the `onlyOwner`

   * modifier anymore.

   */

  function renounceOwnership() public onlyOwner {

    emit OwnershipRenounced(owner);

    owner = address(0);

  }



  /**

   * @dev Allows the current owner to transfer control of the contract to a newOwner.

   * @param _newOwner The address to transfer ownership to.

   */

  function transferOwnership(address _newOwner) public onlyOwner {

    _transferOwnership(_newOwner);

  }



  /**

   * @dev Transfers control of the contract to a newOwner.

   * @param _newOwner The address to transfer ownership to.

   */

  function _transferOwnership(address _newOwner) internal {

    require(_newOwner != address(0));

    emit OwnershipTransferred(owner, _newOwner);

    owner = _newOwner;

  }

}



// File: openzeppelin-solidity/contracts/token/ERC20/SafeERC20.sol



/**

 * @title SafeERC20

 * @dev Wrappers around ERC20 operations that throw on failure.

 * To use this library you can add a `using SafeERC20 for ERC20;` statement to your contract,

 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.

 */

library SafeERC20 {

  function safeTransfer(

    ERC20Basic _token,

    address _to,

    uint256 _value

  )

    internal

  {

    require(_token.transfer(_to, _value));

  }



  function safeTransferFrom(

    ERC20 _token,

    address _from,

    address _to,

    uint256 _value

  )

    internal

  {

    require(_token.transferFrom(_from, _to, _value));

  }



  function safeApprove(

    ERC20 _token,

    address _spender,

    uint256 _value

  )

    internal

  {

    require(_token.approve(_spender, _value));

  }

}



// File: openzeppelin-solidity/contracts/ownership/CanReclaimToken.sol



/**

 * @title Contracts that should be able to recover tokens

 * @author SylTi

 * @dev This allow a contract to recover any ERC20 token received in a contract by transferring the balance to the contract owner.

 * This will prevent any accidental loss of tokens.

 */

contract CanReclaimToken is Ownable {

  using SafeERC20 for ERC20Basic;



  /**

   * @dev Reclaim all ERC20Basic compatible tokens

   * @param _token ERC20Basic The address of the token contract

   */

  function reclaimToken(ERC20Basic _token) external onlyOwner {

    uint256 balance = _token.balanceOf(this);

    _token.safeTransfer(owner, balance);

  }



}



// File: contracts/registry/MultiChanger.sol



contract IEtherToken is ERC20 {

    function deposit() public payable;

    function withdraw(uint256 _amount) public;

}





contract IBancorNetwork {

    function convert(

        address[] _path,

        uint256 _amount,

        uint256 _minReturn

    ) 

        public

        payable

        returns(uint256);



    function claimAndConvert(

        address[] _path,

        uint256 _amount,

        uint256 _minReturn

    ) 

        public

        payable

        returns(uint256);

}





contract IKyberNetworkProxy {

    function trade(

        address src,

        uint srcAmount,

        address dest,

        address destAddress,

        uint maxDestAmount,

        uint minConversionRate,

        address walletId

    )

        public

        payable

        returns(uint);

}





contract MultiChanger is CanReclaimToken {

    using SafeMath for uint256;

    using CheckedERC20 for ERC20;



    // Source: https://github.com/gnosis/MultiSigWallet/blob/master/contracts/MultiSigWallet.sol

    // call has been separated into its own function in order to take advantage

    // of the Solidity's code generator to produce a loop that copies tx.data into memory.

    function externalCall(address destination, uint value, bytes data, uint dataOffset, uint dataLength) internal returns (bool result) {

        // solium-disable-next-line security/no-inline-assembly

        assembly {

            let x := mload(0x40)   // "Allocate" memory for output (0x40 is where "free memory" pointer is stored by convention)

            let d := add(data, 32) // First 32 bytes are the padded length of data, so exclude that

            result := call(

                sub(gas, 34710),   // 34710 is the value that solidity is currently emitting

                                   // It includes callGas (700) + callVeryLow (3, to pay for SUB) + callValueTransferGas (9000) +

                                   // callNewAccountGas (25000, in case the destination address does not exist and needs creating)

                destination,

                value,

                add(d, dataOffset),

                dataLength,        // Size of the input (in bytes) - this is what fixes the padding problem

                x,

                0                  // Output is ignored, therefore the output size is zero

            )

        }

    }



    function change(

        bytes _callDatas,

        uint[] _starts // including 0 and LENGTH values

    )

        internal

    {

        for (uint i = 0; i < _starts.length - 1; i++) {

            require(externalCall(this, 0, _callDatas, _starts[i], _starts[i + 1] - _starts[i]));

        }

    }



    function sendEthValue(address _target, bytes _data, uint256 _value) external {

        // solium-disable-next-line security/no-call-value

        require(_target.call.value(_value)(_data));

    }



    function sendEthProportion(address _target, bytes _data, uint256 _mul, uint256 _div) external {

        uint256 value = address(this).balance.mul(_mul).div(_div);

        // solium-disable-next-line security/no-call-value

        require(_target.call.value(value)(_data));

    }



    function approveTokenAmount(address _target, bytes _data, ERC20 _fromToken, uint256 _amount) external {

        if (_fromToken.allowance(this, _target) != 0) {

            _fromToken.asmApprove(_target, 0);

        }

        _fromToken.asmApprove(_target, _amount);

        // solium-disable-next-line security/no-low-level-calls

        require(_target.call(_data));

    }



    function approveTokenProportion(address _target, bytes _data, ERC20 _fromToken, uint256 _mul, uint256 _div) external {

        uint256 amount = _fromToken.balanceOf(this).mul(_mul).div(_div);

        if (_fromToken.allowance(this, _target) != 0) {

            _fromToken.asmApprove(_target, 0);

        }

        _fromToken.asmApprove(_target, amount);

        // solium-disable-next-line security/no-low-level-calls

        require(_target.call(_data));

    }



    function transferTokenAmount(address _target, bytes _data, ERC20 _fromToken, uint256 _amount) external {

        _fromToken.asmTransfer(_target, _amount);

        // solium-disable-next-line security/no-low-level-calls

        require(_target.call(_data));

    }



    function transferTokenProportion(address _target, bytes _data, ERC20 _fromToken, uint256 _mul, uint256 _div) external {

        uint256 amount = _fromToken.balanceOf(this).mul(_mul).div(_div);

        _fromToken.asmTransfer(_target, amount);

        // solium-disable-next-line security/no-low-level-calls

        require(_target.call(_data));

    }



    // Ether token



    function withdrawEtherTokenAmount(IEtherToken _etherToken, uint256 _amount) external {

        _etherToken.withdraw(_amount);

    }



    function withdrawEtherTokenProportion(IEtherToken _etherToken, uint256 _mul, uint256 _div) external {

        uint256 amount = _etherToken.balanceOf(this).mul(_mul).div(_div);

        _etherToken.withdraw(amount);

    }



    // Bancor Network



    function bancorSendEthValue(IBancorNetwork _bancor, address[] _path, uint256 _value) external {

        _bancor.convert.value(_value)(_path, _value, 1);

    }



    function bancorSendEthProportion(IBancorNetwork _bancor, address[] _path, uint256 _mul, uint256 _div) external {

        uint256 value = address(this).balance.mul(_mul).div(_div);

        _bancor.convert.value(value)(_path, value, 1);

    }



    function bancorApproveTokenAmount(IBancorNetwork _bancor, address[] _path, uint256 _amount) external {

        if (ERC20(_path[0]).allowance(this, _bancor) == 0) {

            ERC20(_path[0]).asmApprove(_bancor, uint256(-1));

        }

        _bancor.claimAndConvert(_path, _amount, 1);

    }



    function bancorApproveTokenProportion(IBancorNetwork _bancor, address[] _path, uint256 _mul, uint256 _div) external {

        uint256 amount = ERC20(_path[0]).balanceOf(this).mul(_mul).div(_div);

        if (ERC20(_path[0]).allowance(this, _bancor) == 0) {

            ERC20(_path[0]).asmApprove(_bancor, uint256(-1));

        }

        _bancor.claimAndConvert(_path, amount, 1);

    }



    function bancorTransferTokenAmount(IBancorNetwork _bancor, address[] _path, uint256 _amount) external {

        ERC20(_path[0]).asmTransfer(_bancor, _amount);

        _bancor.convert(_path, _amount, 1);

    }



    function bancorTransferTokenProportion(IBancorNetwork _bancor, address[] _path, uint256 _mul, uint256 _div) external {

        uint256 amount = ERC20(_path[0]).balanceOf(this).mul(_mul).div(_div);

        ERC20(_path[0]).asmTransfer(_bancor, amount);

        _bancor.convert(_path, amount, 1);

    }



    function bancorAlreadyTransferedTokenAmount(IBancorNetwork _bancor, address[] _path, uint256 _amount) external {

        _bancor.convert(_path, _amount, 1);

    }



    function bancorAlreadyTransferedTokenProportion(IBancorNetwork _bancor, address[] _path, uint256 _mul, uint256 _div) external {

        uint256 amount = ERC20(_path[0]).balanceOf(_bancor).mul(_mul).div(_div);

        _bancor.convert(_path, amount, 1);

    }



    // Kyber Network



    function kyberSendEthProportion(IKyberNetworkProxy _kyber, ERC20 _fromToken, address _toToken, uint256 _mul, uint256 _div) external {

        uint256 value = address(this).balance.mul(_mul).div(_div);

        _kyber.trade.value(value)(

            _fromToken,

            value,

            _toToken,

            this,

            1 << 255,

            0,

            0

        );

    }



    function kyberApproveTokenAmount(IKyberNetworkProxy _kyber, ERC20 _fromToken, address _toToken, uint256 _amount) external {

        if (_fromToken.allowance(this, _kyber) == 0) {

            _fromToken.asmApprove(_kyber, uint256(-1));

        }

        _kyber.trade(

            _fromToken,

            _amount,

            _toToken,

            this,

            1 << 255,

            0,

            0

        );

    }



    function kyberApproveTokenProportion(IKyberNetworkProxy _kyber, ERC20 _fromToken, address _toToken, uint256 _mul, uint256 _div) external {

        uint256 amount = _fromToken.balanceOf(this).mul(_mul).div(_div);

        this.kyberApproveTokenAmount(_kyber, _fromToken, _toToken, amount);

    }

}



// File: contracts/registry/MultiSeller.sol



contract MultiSeller is MultiChanger {

    using CheckedERC20 for ERC20;

    using CheckedERC20 for IMultiToken;



    function() public payable {

        // solium-disable-next-line security/no-tx-origin

        require(tx.origin != msg.sender);

    }



    function sellForOrigin(

        IMultiToken _mtkn,

        uint256 _amount,

        bytes _callDatas,

        uint[] _starts // including 0 and LENGTH values

    )

        public

    {

        sell(

            _mtkn,

            _amount,

            _callDatas,

            _starts,

            tx.origin   // solium-disable-line security/no-tx-origin

        );

    }



    function sell(

        IMultiToken _mtkn,

        uint256 _amount,

        bytes _callDatas,

        uint[] _starts, // including 0 and LENGTH values

        address _for

    )

        public

    {

        _mtkn.asmTransferFrom(msg.sender, this, _amount);

        _mtkn.unbundle(this, _amount);

        change(_callDatas, _starts);

        _for.transfer(address(this).balance);

    }



    // DEPRECATED:



    function sellOnApproveForOrigin(

        IMultiToken _mtkn,

        uint256 _amount,

        ERC20 _throughToken,

        address[] _exchanges,

        bytes _datas,

        uint[] _datasIndexes // including 0 and LENGTH values

    )

        public

    {

        sellOnApprove(

            _mtkn,

            _amount,

            _throughToken,

            _exchanges,

            _datas,

            _datasIndexes,

            tx.origin

        );

    }



    function sellOnApprove(

        IMultiToken _mtkn,

        uint256 _amount,

        ERC20 _throughToken,

        address[] _exchanges,

        bytes _datas,

        uint[] _datasIndexes, // including 0 and LENGTH values

        address _for

    )

        public

    {

        if (_throughToken == address(0)) {

            require(_mtkn.tokensCount() == _exchanges.length, "sell: _mtkn should have the same tokens count as _exchanges");

        } else {

            require(_mtkn.tokensCount() + 1 == _exchanges.length, "sell: _mtkn should have tokens count + 1 equal _exchanges length");

        }

        require(_datasIndexes.length == _exchanges.length + 1, "sell: _datasIndexes should start with 0 and end with LENGTH");



        _mtkn.transferFrom(msg.sender, this, _amount);

        _mtkn.unbundle(this, _amount);



        for (uint i = 0; i < _exchanges.length; i++) {

            bytes memory data = new bytes(_datasIndexes[i + 1] - _datasIndexes[i]);

            for (uint j = _datasIndexes[i]; j < _datasIndexes[i + 1]; j++) {

                data[j - _datasIndexes[i]] = _datas[j];

            }

            if (data.length == 0) {

                continue;

            }



            if (i == _exchanges.length - 1 && _throughToken != address(0)) {

                if (_throughToken.allowance(this, _exchanges[i]) == 0) {

                    _throughToken.asmApprove(_exchanges[i], uint256(-1));

                }

            } else {

                ERC20 token = _mtkn.tokens(i);

                if (_exchanges[i] == 0) {

                    token.asmTransfer(_for, token.balanceOf(this));

                    continue;

                }

                token.asmApprove(_exchanges[i], token.balanceOf(this));

            }

            require(_exchanges[i].call(data), "sell: exchange arbitrary call failed");

        }



        _for.transfer(address(this).balance);

        if (_throughToken != address(0) && _throughToken.balanceOf(this) > 0) {

            _throughToken.asmTransfer(_for, _throughToken.balanceOf(this));

        }

    }

}