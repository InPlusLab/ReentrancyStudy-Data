// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

pragma solidity ^0.4.23;

// import 'ds-auth/auth.sol';
contract DSAuthority {
    function canCall(
        address src, address dst, bytes4 sig
    ) public view returns (bool);
}

contract DSAuthEvents {
    event LogSetAuthority (address indexed authority);
    event LogSetOwner     (address indexed owner);
}

contract DSAuth is DSAuthEvents {
    DSAuthority  public  authority;
    address      public  owner;

    constructor() public {
        owner = msg.sender;
        emit LogSetOwner(msg.sender);
    }

    function setOwner(address owner_)
        public
        auth
    {
        owner = owner_;
        emit LogSetOwner(owner);
    }

    function setAuthority(DSAuthority authority_)
        public
        auth
    {
        authority = authority_;
        emit LogSetAuthority(authority);
    }

    modifier auth {
        require(isAuthorized(msg.sender, msg.sig));
        _;
    }

    function isAuthorized(address src, bytes4 sig) internal view returns (bool) {
        if (src == address(this)) {
            return true;
        } else if (src == owner) {
            return true;
        } else if (authority == DSAuthority(0)) {
            return false;
        } else {
            return authority.canCall(src, this, sig);
        }
    }
}

// import 'ds-math/math.sol';
contract DSMath {
    function add(uint x, uint y) internal pure returns (uint z) {
        require((z = x + y) >= x);
    }
    function sub(uint x, uint y) internal pure returns (uint z) {
        require((z = x - y) <= x);
    }
    function mul(uint x, uint y) internal pure returns (uint z) {
        require(y == 0 || (z = x * y) / y == x);
    }

    function min(uint x, uint y) internal pure returns (uint z) {
        return x <= y ? x : y;
    }
    function max(uint x, uint y) internal pure returns (uint z) {
        return x >= y ? x : y;
    }
    function imin(int x, int y) internal pure returns (int z) {
        return x <= y ? x : y;
    }
    function imax(int x, int y) internal pure returns (int z) {
        return x >= y ? x : y;
    }

    uint constant WAD = 10 ** 18;
    uint constant RAY = 10 ** 27;

    function wmul(uint x, uint y) internal pure returns (uint z) {
        z = add(mul(x, y), WAD / 2) / WAD;
    }
    function rmul(uint x, uint y) internal pure returns (uint z) {
        z = add(mul(x, y), RAY / 2) / RAY;
    }
    function wdiv(uint x, uint y) internal pure returns (uint z) {
        z = add(mul(x, WAD), y / 2) / y;
    }
    function rdiv(uint x, uint y) internal pure returns (uint z) {
        z = add(mul(x, RAY), y / 2) / y;
    }

    // This famous algorithm is called "exponentiation by squaring"
    // and calculates x^n with x as fixed-point and n as regular unsigned.
    //
    // It's O(log n), instead of O(n) for naive repeated multiplication.
    //
    // These facts are why it works:
    //
    //  If n is even, then x^n = (x^2)^(n/2).
    //  If n is odd,  then x^n = x * x^(n-1),
    //   and applying the equation for even x gives
    //    x^n = x * (x^2)^((n-1) / 2).
    //
    //  Also, EVM division is flooring and
    //    floor[(n-1) / 2] = floor[n / 2].
    //
    function rpow(uint x, uint n) internal pure returns (uint z) {
        z = n % 2 != 0 ? x : RAY;

        for (n /= 2; n != 0; n /= 2) {
            x = rmul(x, x);

            if (n % 2 != 0) {
                z = rmul(z, x);
            }
        }
    }
}

// import './IkuraStorage.sol';
/**
 *
 * ロジックの更新に影されない永A化デ`タを保持するクラス
 *
 */
contract IkuraStorage is DSMath, DSAuth {
  // オ`ナ`（中央y行）のアドレス
  address[] ownerAddresses;

  // 各アドレスのdJPYの口座残高
  mapping(address => uint) coinBalances;

  // 各アドレスのSHINJI tokenの口座残高
  mapping(address => uint) tokenBalances;

  // 各アドレスが指定したアドレスにしてS可する最大送金~
  mapping(address => mapping (address => uint)) coinAllowances;

  // dJPYのk行高
  uint _totalSupply = 0;

  // 手数料率
  // 0.01pips = 1
  // 例). 手数料を 0.05% とする龊悉 500
  uint _transferFeeRate = 500;

  // 最低手数料~
  // 1 = 1dJPY
  // amount * 手数料率で算出した金~がここでO定した最低手数料を下回る龊悉稀⒆畹褪质料~を手数料とする
  uint8 _transferMinimumFee = 5;

  address tokenAddress;
  address multiSigAddress;
  address authorityAddress;

  // @NOTE リリ`スrにcontractのdeploy -> watch contract -> setOwnerの流れを
  //省略したい龊悉稀ここで直接controllerのアドレスを指定するとショ`トカットできます
  // 勿テストは通らなくなるので、テストが通ったらしてね
  constructor() public DSAuth() {
    /*address controllerAddress = 0x34c5605A4Ef1C98575DB6542179E55eE1f77A188;
    owner = controllerAddress;
    LogSetOwner(controllerAddress);*/
  }

  function changeToken(address tokenAddress_) public auth {
    tokenAddress = tokenAddress_;
  }

  function changeAssociation(address multiSigAddress_) public auth {
    multiSigAddress = multiSigAddress_;
  }

  function changeAuthority(address authorityAddress_) public auth {
    authorityAddress = authorityAddress_;
  }

  // --------------------------------------------------
  // functions for _totalSupply
  // --------------------------------------------------

  /**
   * tk行~を返す
   *
   * @return tk行~
   */
  function totalSupply() public view auth returns (uint) {
    return _totalSupply;
  }

  /**
   * tk行数をやす（mintとK行して呼ばれることを想定）
   *
   * @param amount 造数
   */
  function addTotalSupply(uint amount) public auth {
    _totalSupply = add(_totalSupply, amount);
  }

  /**
   * tk行数をpらす（burnとK行して呼ばれることを想定）
   *
   * @param amount 造数
   */
  function subTotalSupply(uint amount) public auth {
    _totalSupply = sub(_totalSupply, amount);
  }

  // --------------------------------------------------
  // functions for _transferFeeRate
  // --------------------------------------------------

  /**
   * 手数料率を返す
   *
   * @return F在の手数料率
   */
  function transferFeeRate() public view auth returns (uint) {
    return _transferFeeRate;
  }

  /**
   * 手数料率を涓する
   *
   * @param newTransferFeeRate 新しい手数料率
   *
   * @return 更新に成功したらtrue、失・筏郡false（今のところ失・工毳暴`スはない）
   */
  function setTransferFeeRate(uint newTransferFeeRate) public auth returns (bool) {
    _transferFeeRate = newTransferFeeRate;

    return true;
  }

  // --------------------------------------------------
  // functions for _transferMinimumFee
  // --------------------------------------------------

  /**
   * 最低手数料返す
   *
   * @return F在の最低手数料
   */
  function transferMinimumFee() public view auth returns (uint8) {
    return _transferMinimumFee;
  }

  /**
   * 最低手数料を涓する
   *
   * @param newTransferMinimumFee 新しい最低手数料
   *
   * @return 更新に成功したらtrue、失・筏郡false（今のところ失・工毳暴`スはない）
   */
  function setTransferMinimumFee(uint8 newTransferMinimumFee) public auth {
    _transferMinimumFee = newTransferMinimumFee;
  }

  // --------------------------------------------------
  // functions for ownerAddresses
  // --------------------------------------------------

  /**
   * 指定したユ`ザ`アドレスをオ`ナ`の一Eに追加する
   *
   * ト`クンの移rに内部的にオ`ナ`のアドレスを管理するためのv数。
   * ト`クンの所有者 = オ`ナ`というQいになったので、この配列に含まれるアドレスの一Eは
   * 手数料からのб妞畏峙浃颏工rに利用するだけで、オ`ナ`かどうかの判定には利用しない
   *
   * @param addr ユ`ザ`のアドレス
   *
   * @return I理に成功したらtrue、失・筏郡false
   */
  function addOwnerAddress(address addr) internal returns (bool) {
    ownerAddresses.push(addr);

    return true;
  }

  /**
   * 指定したユ`ザ`アドレスをオ`ナ`の一Eから削除する
   *
   * ト`クンの移rに内部的にオ`ナ`のアドレスを管理するためのv数。
   *
   * @param addr オ`ナ`に属するユ`ザ`のアドレス
   *
   * @return I理に成功したらtrue、失・筏郡false
   */
  function removeOwnerAddress(address addr) internal returns (bool) {
    uint i = 0;

    while (ownerAddresses[i] != addr) { i++; }

    while (i < ownerAddresses.length - 1) {
      ownerAddresses[i] = ownerAddresses[i + 1];
      i++;
    }

    ownerAddresses.length--;

    return true;
  }

  /**
   * 最初のオ`ナ`（contractをdeployしたユ`ザ`）のアドレスを返す
   *
   * @return 最初のオ`ナ`のアドレス
   */
  function primaryOwner() public view auth returns (address) {
    return ownerAddresses[0];
  }

  /**
   * 指定したアドレスがオ`ナ`アドレスに登hされているか返す
   *
   * @param addr ユ`ザ`のアドレス
   *
   * @return オ`ナ`に含まれている龊悉true、含まれていない龊悉false
   */
  function isOwnerAddress(address addr) public view auth returns (bool) {
    for (uint i = 0; i < ownerAddresses.length; i++) {
      if (ownerAddresses[i] == addr) return true;
    }

    return false;
  }

  /**
   * オ`ナ`数を返す
   *
   * @return オ`ナ`数
   */
  function numOwnerAddress() public view auth returns (uint) {
    return ownerAddresses.length;
  }

  // --------------------------------------------------
  // functions for coinBalances
  // --------------------------------------------------

  /**
   * 指定したユ`ザ`のdJPY残高を返す
   *
   * @param addr ユ`ザ`のアドレス
   *
   * @return dJPY残高
   */
  function coinBalance(address addr) public view auth returns (uint) {
    return coinBalances[addr];
  }

  /**
   * 指定したユ`ザ`のdJPYの残高をやす
   *
   * @param addr ユ`ザ`のアドレス
   * @param amount 差分
   *
   * @return I理に成功したらtrue、失・筏郡false
   */
  function addCoinBalance(address addr, uint amount) public auth returns (bool) {
    coinBalances[addr] = add(coinBalances[addr], amount);

    return true;
  }

  /**
   * 指定したユ`ザ`のdJPYの残高をpらす
   *
   * @param addr ユ`ザ`のアドレス
   * @param amount 差分
   *
   * @return I理に成功したらtrue、失・筏郡false
   */
  function subCoinBalance(address addr, uint amount) public auth returns (bool) {
    coinBalances[addr] = sub(coinBalances[addr], amount);

    return true;
  }

  // --------------------------------------------------
  // functions for tokenBalances
  // --------------------------------------------------

  /**
   * 指定したユ`ザ`のSHINJIト`クンの残高を返す
   *
   * @param addr ユ`ザ`のアドレス
   *
   * @return SHINJIト`クン残高
   */
  function tokenBalance(address addr) public view auth returns (uint) {
    return tokenBalances[addr];
  }

  /**
   * 指定したユ`ザ`のSHINJIト`クンの残高をやす
   *
   * @param addr ユ`ザ`のアドレス
   * @param amount 差分
   *
   * @return I理に成功したらtrue、失・筏郡false
   */
  function addTokenBalance(address addr, uint amount) public auth returns (bool) {
    tokenBalances[addr] = add(tokenBalances[addr], amount);

    if (tokenBalances[addr] > 0 && !isOwnerAddress(addr)) {
      addOwnerAddress(addr);
    }

    return true;
  }

  /**
   * 指定したユ`ザ`のSHINJIト`クンの残高をpらす
   *
   * @param addr ユ`ザ`のアドレス
   * @param amount 差分
   *
   * @return I理に成功したらtrue、失・筏郡false
   */
  function subTokenBalance(address addr, uint amount) public auth returns (bool) {
    tokenBalances[addr] = sub(tokenBalances[addr], amount);

    if (tokenBalances[addr] <= 0) {
      removeOwnerAddress(addr);
    }

    return true;
  }

  // --------------------------------------------------
  // functions for coinAllowances
  // --------------------------------------------------

  /**
   * 送金S可金~を返す
   *
   * @param owner_ 送金者
   * @param spender 送金代行者
   *
   * @return 送金S可金~
   */
  function coinAllowance(address owner_, address spender) public view auth returns (uint) {
    return coinAllowances[owner_][spender];
  }

  /**
   * 送金S可金~を指定した金~だけやす
   *
   * @param owner_ 送金者
   * @param spender 送金代行者
   * @param amount 金~
   *
   * @return 更新に成功したらtrue、失・筏郡false
   */
  function addCoinAllowance(address owner_, address spender, uint amount) public auth returns (bool) {
    coinAllowances[owner_][spender] = add(coinAllowances[owner_][spender], amount);

    return true;
  }

  /**
   * 送金S可金~を指定した金~だけpらす
   *
   * @param owner_ 送金者
   * @param spender 送金代行者
   * @param amount 金~
   *
   * @return 更新に成功したらtrue、失・筏郡false
   */
  function subCoinAllowance(address owner_, address spender, uint amount) public auth returns (bool) {
    coinAllowances[owner_][spender] = sub(coinAllowances[owner_][spender], amount);

    return true;
  }

  /**
   * 送金S可金~を指定したに更新する
   *
   * @param owner_ 送金者
   * @param spender 送金代行者
   * @param amount 送金S可金~
   *
   * @return 指定に成功したらtrue、失・筏郡false
   */
  function setCoinAllowance(address owner_, address spender, uint amount) public auth returns (bool) {
    coinAllowances[owner_][spender] = amount;

    return true;
  }

  /**
   * 叵蕙隶Д氓用v数のoverride
   *
   * @param src g行者アドレス
   * @param sig g行v数のRe子
   *
   * @return g行がS可されていればtrue、そうでなければfalse
   */
  function isAuthorized(address src, bytes4 sig) internal view returns (bool) {
    sig; // #HACK

    return  src == address(this) ||
            src == owner ||
            src == tokenAddress ||
            src == authorityAddress ||
            src == multiSigAddress;
  }
}


// import './IkuraTokenEvent.sol';
/**
 * TokenでのI理にvするイベント定x
 *
 * - ERC20にしたイベント（Transfer / Approval）
 * - IkuraTokenの独自イベント（TransferToken / TransferFee）
 */
contract IkuraTokenEvent {
  /** オ`ナ`がdJPYを造したHにk火するイベント */
  event IkuraMint(address indexed owner, uint);

  /** オ`ナ`がdJPYを消却したHにk火するイベント */
  event IkuraBurn(address indexed owner, uint);

  /** ト`クンの移rにk火するイベント */
  event IkuraTransferToken(address indexed from, address indexed to, uint value);

  /** 手数料がk生したときにk火するイベント */
  event IkuraTransferFee(address indexed from, address indexed to, address indexed owner, uint value);

  /**
   * テストrにこのイベントも流れてくるはずなので追加で定x
   * controllerでもイベントをk火させているが、ゆくゆくはここでIkuraTokenのバ`ジョンとか追加の情螭蛲钉菠肟赡苄预猡るので残留。
   */
  event IkuraTransfer(address indexed from, address indexed to, uint value);

  /** 送金S可イベント */
  event IkuraApproval(address indexed owner, address indexed spender, uint value);
}


// import './IkuraToken.sol';
/**
 *
 * ト`クンロジック
 *
 */
contract IkuraToken is IkuraTokenEvent, DSMath, DSAuth {
  //
  // constants
  //

  // 手数料率
  // 0.01pips = 1
  // 例). 手数料を 0.05% とする龊悉 500
  uint _transferFeeRate = 0;

  // 最低手数料~
  // 1 = 1dJPY
  // amount * 手数料率で算出した金~がここでO定した最低手数料を下回る龊悉稀⒆畹褪质料~を手数料とする
  uint8 _transferMinimumFee = 0;

  // ロジックバ`ジョン
  uint _logicVersion = 2;

  //
  // libraries
  //

  /*using ProposalLibrary for ProposalLibrary.Entity;
  ProposalLibrary.Entity proposalEntity;*/

  //
  // private
  //

  // デ`タの永A化ストレ`ジ
  IkuraStorage _storage;
  IkuraAssociation _association;

  constructor() DSAuth() public {
    // @NOTE リリ`スrにcontractのdeploy -> watch contract -> setOwnerの流れを
    //省略したい龊悉稀ここで直接controllerのアドレスを指定するとショ`トカットできます
    // 勿テストは通らなくなるので、テストが通ったらしてね
    /*address controllerAddress = 0x34c5605A4Ef1C98575DB6542179E55eE1f77A188;
    owner = controllerAddress;
    LogSetOwner(controllerAddress);*/
  }

  // ----------------------------------------------------------------------------------------------------
  // 以降はERC20にしたv数
  // ----------------------------------------------------------------------------------------------------

  /**
   * ERC20 Token Standardにしたv数
   *
   * dJPYのk行高を返す
   *
   * @return k行高
   */
  function totalSupply(address sender) public view returns (uint) {
    sender; // #HACK

    return _storage.totalSupply();
  }

  /**
   * ERC20 Token Standardにしたv数
   *
   * 特定のアドレスのdJPY残高を返す
   *
   * @param sender g行アドレス
   * @param addr アドレス
   *
   * @return 指定したアドレスのdJPY残高
   */
  function balanceOf(address sender, address addr) public view returns (uint) {
    sender; // #HACK

    return _storage.coinBalance(addr);
  }

  /**
   * ERC20 Token Standardにしたv数
   *
   * 指定したアドレスにしてdJPYを送金する
   * 以下の条件を氦郡贡匾がある
   *
   * - メッセ`ジの送信者の残高 >= 送金~
   * - 送金~ > 0
   * - 送金先のアドレスの残高 + 送金~ > 送金元のアドレスの残高（overflowのチェックらしい）
   *
   * @param sender 送金元アドレス
   * @param to 送金象アドレス
   * @param amount 送金~
   *
   * @return 条件を氦郡筏I理に成功した龊悉true、失・筏龊悉false
   */
  function transfer(address sender, address to, uint amount) public auth returns (bool success) {
    uint fee = transferFee(sender, sender, to, amount);
    uint totalAmount = add(amount, fee);

    require(_storage.coinBalance(sender) >= totalAmount);
    require(amount > 0);

    // g行者の口座からamount + feeの金~が控除される
    _storage.subCoinBalance(sender, totalAmount);

    // toの口座にamountが振りzまれる
    _storage.addCoinBalance(to, amount);

    if (fee > 0) {
      // 手数料を受け取るオ`ナ`のアドレスをx定
      address owner = selectOwnerAddressForTransactionFee(sender);

      // オ`ナ`の口座にfeeが振りzまれる
      _storage.addCoinBalance(owner, fee);
    }

    return true;
  }

  /**
   * ERC20 Token Standardにしたv数
   *
   * from（送信元のアドレス）からto（送信先のアドレス）へamount分だけ送金する。
   * 主に口座からの引き出しに利用され、契sによってサブ通の送金手数料をГ工毪长趣できるようになる。
   * この操作はfrom（送信元のアドレス）が何らかの方法で意淼膜怂托耪撙蛟S可する龊悉虺いて失・工伽。
   * このS可するI理はapproveコマンドでg装しましょう。
   *
   * 以下の条件を氦郡龊悉坤彼徒黏蛘Jめる
   *
   * - 送信元の残高 >= 金~
   * - 送金する金~ > 0
   * - 送信者にして送信元がS可している金~ >= 送金する金~
   * - 送信先の残高 + 金~ > 送信元の残高（overflowのチェックらしい）
   # - 送金I理を行うユ`ザ`の口座残高 >= 送金I理の手数料
   *
   * @param sender g行アドレス
   * @param from 送金元アドレス
   * @param to 送金先アドレス
   * @param amount 送金~
   *
   * @return 条件を氦郡筏I理に成功した龊悉true、失・筏龊悉false
   */
  function transferFrom(address sender, address from, address to, uint amount) public auth returns (bool success) {
    uint fee = transferFee(sender, from, to, amount);

    require(_storage.coinBalance(from) >= amount);
    require(_storage.coinAllowance(from, sender) >= amount);
    require(amount > 0);
    require(add(_storage.coinBalance(to), amount) > _storage.coinBalance(to));

    if (fee > 0) {
      require(_storage.coinBalance(sender) >= fee);

      // 手数料を受け取るオ`ナ`のアドレスをx定
      address owner = selectOwnerAddressForTransactionFee(sender);

      // 手数料はこのv数をg行したユ`ザ`（主に取引所とか）からГ工
      _storage.subCoinBalance(sender, fee);

      _storage.addCoinBalance(owner, fee);
    }

    // 送金元から送金~を引く
    _storage.subCoinBalance(from, amount);

    // 送金S可している金~をpらす
    _storage.subCoinAllowance(from, sender, amount);

    // 送金口座に送金~を足す
    _storage.addCoinBalance(to, amount);

    return true;
  }

  /**
   * ERC20 Token Standardにしたv数
   *
   * spender（支Bい元のアドレス）にsender（送信者）がamount分だけ支BうのをS可する
   * このv数が呼ばれる度に送金可能な金~を更新する。
   *
   * @param sender g行アドレス
   * @param spender 送金元アドレス
   * @param amount 送金~
   *
   * @return 基本的にtrueを返す
   */
  function approve(address sender, address spender, uint amount) public auth returns (bool success) {
    _storage.setCoinAllowance(sender, spender, amount);

    return true;
  }

  /**
   * ERC20 Token Standardにしたv数
   *
   * 受取趣して支Bい趣どれだけ送金可能かを返す
   *
   * @param sender g行アドレス
   * @param owner 受け取り趣违ドレス
   * @param spender 支Bい元のアドレス
   *
   * @return S可されている送金料
   */
  function allowance(address sender, address owner, address spender) public view returns (uint remaining) {
    sender; // #HACK

    return _storage.coinAllowance(owner, spender);
  }

  // ----------------------------------------------------------------------------------------------------
  // 以降はERC20以外の独自g装
  // ----------------------------------------------------------------------------------------------------

  /**
   * 特定のアドレスのSHINJI残高を返す
   *
   * @param sender g行アドレス
   * @param owner アドレス
   *
   * @return 指定したアドレスのSHINJIト`クン量
   */
  function tokenBalanceOf(address sender, address owner) public view returns (uint balance) {
    sender; // #HACK

    return _storage.tokenBalance(owner);
  }

  /**
   * 指定したアドレスにしてSHINJIト`クンを送金する
   *
   * - 送信元の残ト`クン量 >= ト`クン量
   * - 送信するト`クン量 > 0
   * - 送信先の残高 + 金~ > 送信元の残高（overflowのチェック）
   *
   * @param sender g行アドレス
   * @param to 送金象アドレス
   * @param amount 送金~
   *
   * @return 条件を氦郡筏I理に成功した龊悉true、失・筏龊悉false
   */
  function transferToken(address sender, address to, uint amount) public auth returns (bool success) {
    require(_storage.tokenBalance(sender) >= amount);
    require(amount > 0);
    require(add(_storage.tokenBalance(to), amount) > _storage.tokenBalance(to));

    _storage.subTokenBalance(sender, amount);
    _storage.addTokenBalance(to, amount);

    emit IkuraTransferToken(sender, to, amount);

    return true;
  }

  /**
   * 送金元、送金先、送金金~によって象のトランザクションの手数料をQ定する
   * 送金金~にして手数料率をかけたものを算し、最低手数料金~とのmaxを取る。
   *
   * @param sender g行アドレス
   * @param from 送金元
   * @param to 送金先
   * @param amount 送金金~
   *
   * @return 手数料金~
   */
  function transferFee(address sender, address from, address to, uint amount) public view returns (uint) {
    sender; from; to; // #avoid warning
    if (_transferFeeRate > 0) {
      uint denominator = 1000000; // 0.01 pips だから 100 * 100 * 100 で 100万
      uint numerator = mul(amount, _transferFeeRate);

      uint fee = numerator / denominator;
      uint remainder = sub(numerator, mul(denominator, fee));

      // 余りがある龊悉feeに1を足す
      if (remainder > 0) {
        fee++;
      }

      if (fee < _transferMinimumFee) {
        fee = _transferMinimumFee;
      }

      return fee;
    } else {
      return 0;
    }
  }

  /**
   * 手数料率を返す
   *
   * @param sender g行アドレス
   *
   * @return 手数料率
   */
  function transferFeeRate(address sender) public view returns (uint) {
    sender; // #HACK

    return _transferFeeRate;
  }

  /**
   * 最低手数料~を返す
   *
   * @param sender g行アドレス
   *
   * @return 最低手数料~
   */
  function transferMinimumFee(address sender) public view returns (uint8) {
    sender; // #HACK

    return _transferMinimumFee;
  }

  /**
   * 手数料を振りzむ口座をxkする
   * #TODO とりあえず一目のオ`ナ`に固定。後でx定ロジックを浃à
   *
   * @param sender g行アドレス
   * @return 特定のオ`ナ`口座
   */
  function selectOwnerAddressForTransactionFee(address sender) public view returns (address) {
    sender; // #HACK

    return _storage.primaryOwner();
  }

  /**
   * dJPYを造する
   *
   * - コマンドをg行したユ`ザがオ`ナ`である
   * - 造する量が0より大きい
   *
   * 龊悉铣晒Δ工
   *
   * @param sender g行アドレス
   * @param amount 造する金~
   */
  function mint(address sender, uint amount) public auth returns (bool) {
    require(amount > 0);

    _association.newProposal(keccak256('mint'), sender, amount, '');

    return true;
    /*return proposalEntity.mint(sender, amount);*/
  }

  /**
   * dJPYを消却する
   *
   * - コマンドをg行したユ`ザがオ`ナ`である
   * - 造する量が0より大きい
   * - dJPYの残高がamountよりも大きい
   * - SHINJIをamountよりも大きい
   *
   * 龊悉铣晒Δ工
   *
   * @param sender g行アドレス
   * @param amount 消却する金~
   */
  function burn(address sender, uint amount) public auth returns (bool) {
    require(amount > 0);
    require(_storage.coinBalance(sender) >= amount);
    require(_storage.tokenBalance(sender) >= amount);

    _association.newProposal(keccak256('burn'), sender, amount, '');

    return true;
    /*return proposalEntity.burn(sender, amount);*/
  }

  /**
   * 提案を承Jする。
   * #TODO proposalIdは分からないので、eのものからproposalを特定しないといかんよ
   */
  function confirmProposal(address sender, bytes32 type_, uint proposalId) public auth {
    _association.confirmProposal(type_, sender, proposalId);
    /*proposalEntity.confirmProposal(sender, type_, proposalId);*/
  }

  /**
   * 指定したNの提案数を取得する
   *
   * @param type_ 提案のN（'mint' | 'burn' | 'transferMinimumFee' | 'transferFeeRate'）
   *
   * @return 提案数（承Jされていないものも含む）
   */
  function numberOfProposals(bytes32 type_) public view returns (uint) {
    return _association.numberOfProposals(type_);
    /*return proposalEntity.numberOfProposals(type_);*/
  }

  /**
   * vBづける承Jプロセスを涓する
   *
   * @param association_ 新しい承Jプロセス
   */
  function changeAssociation(address association_) public auth returns (bool) {
    _association = IkuraAssociation(association_);
    return true;
  }

  /**
   * 永A化ストレ`ジをO定する
   *
   * @param storage_ 永A化ストレ`ジのインスタンス（アドレス）
   */
  function changeStorage(address storage_) public auth returns (bool) {
    _storage = IkuraStorage(storage_);
    return true;
  }

  /**
   * ロジックのバ`ジョンを返す
   *
   * @param sender g行ユ`ザ`のアドレス
   *
   * @return バ`ジョン情
   */
  function logicVersion(address sender) public view returns (uint) {
    sender; // #HACK

    return _logicVersion;
  }
}

/**
 * U^rgとSHINJI Tokenの所有比率によって特定のアクションの承Jを行うクラス
 */
contract IkuraAssociation is DSMath, DSAuth {
  //
  // public
  //

  // 提案が承Jされるために必要なm成票の割合
  uint public confirmTotalTokenThreshold = 50;

  //
  // private
  //

  // デ`タの永A化ストレ`ジ
  IkuraStorage _storage;
  IkuraToken _token;

  // 提案一E
  Proposal[] mintProposals;
  Proposal[] burnProposals;

  mapping (bytes32 => Proposal[]) proposals;

  struct Proposal {
    address proposer;                     // 提案者
    bytes32 digest;                       // チェックサム
    bool executed;                        // g行の有o
    uint createdAt;                       // 提案作成日r
    uint expireAt;                        // 提案のめ切り
    address[] confirmers;                 // 承J者
    uint amount;                          // 造量
  }

  //
  // Events
  //

  event MintProposalAdded(uint proposalId, address proposer, uint amount);
  event MintConfirmed(uint proposalId, address confirmer, uint amount);
  event MintExecuted(uint proposalId, address proposer, uint amount);

  event BurnProposalAdded(uint proposalId, address proposer, uint amount);
  event BurnConfirmed(uint proposalId, address confirmer, uint amount);
  event BurnExecuted(uint proposalId, address proposer, uint amount);

  constructor() public {
    proposals[keccak256('mint')] = mintProposals;
    proposals[keccak256('burn')] = burnProposals;

    // @NOTE リリ`スrにcontractのdeploy -> watch contract -> setOwnerの流れを
    //省略したい龊悉稀ここで直接controllerのアドレスを指定するとショ`トカットできます
    // 勿テストは通らなくなるので、テストが通ったらしてね
    /*address controllerAddress = 0x34c5605A4Ef1C98575DB6542179E55eE1f77A188;
    owner = controllerAddress;
    LogSetOwner(controllerAddress);*/
  }

  /**
   * 永A化ストレ`ジをO定する
   *
   * @param newStorage 永A化ストレ`ジのインスタンス（アドレス）
   */
  function changeStorage(IkuraStorage newStorage) public auth returns (bool) {
    _storage = newStorage;
    return true;
  }

  function changeToken(IkuraToken token_) public auth returns (bool) {
    _token = token_;
    return true;
  }

  /**
   * 提案を作成する
   *
   * @param proposer 提案者のアドレス
   * @param amount 造量
   */
  function newProposal(bytes32 type_, address proposer, uint amount, bytes transationBytecode) public returns (uint) {
    uint proposalId = proposals[type_].length++;
    Proposal storage proposal = proposals[type_][proposalId];
    proposal.proposer = proposer;
    proposal.amount = amount;
    proposal.digest = keccak256(proposer, amount, transationBytecode);
    proposal.executed = false;
    proposal.createdAt = now;
    proposal.expireAt = proposal.createdAt + 86400;

    // 提案のN挨g行すべき内容をg行する
    // @NOTE literal_stringとbytesはgに比^できないのでkeccak256のハッシュで比^している
    if (type_ == keccak256('mint')) emit MintProposalAdded(proposalId, proposer, amount);
    if (type_ == keccak256('burn')) emit BurnProposalAdded(proposalId, proposer, amount);

    // 本人は当然承J
    confirmProposal(type_, proposer, proposalId);

    return proposalId;
  }

  /**
   * ト`クン所有者が提案にしてm成する
   *
   * @param type_ 提案のN
   * @param confirmer 承J者のアドレス
   * @param proposalId 提案ID
   */
  function confirmProposal(bytes32 type_, address confirmer, uint proposalId) public {
    Proposal storage proposal = proposals[type_][proposalId];

    // 既に承Jgみの龊悉膝ē椹`を返す
    require(!hasConfirmed(type_, confirmer, proposalId));

    // 承J行椁蛐肖盲骏榨楗挨蛄てる
    proposal.confirmers.push(confirmer);

    // 提案のN挨g行すべき内容をg行する
    // @NOTE literal_stringとbytesはgに比^できないのでkeccak256のハッシュで比^している
    if (type_ == keccak256('mint')) emit MintConfirmed(proposalId, confirmer, proposal.amount);
    if (type_ == keccak256('burn')) emit BurnConfirmed(proposalId, confirmer, proposal.amount);

    if (isProposalExecutable(type_, proposalId, proposal.proposer, '')) {
      proposal.executed = true;

      // 提案のN挨g行すべき内容をg行する
      // @NOTE literal_stringとbytesはgに比^できないのでkeccak256のハッシュで比^している
      if (type_ == keccak256('mint')) executeMintProposal(proposalId);
      if (type_ == keccak256('burn')) executeBurnProposal(proposalId);
    }
  }

  /**
   * 既に承Jgみの提案かどうかを返す
   *
   * @param type_ 提案のN
   * @param addr 承J者のアドレス
   * @param proposalId 提案ID
   *
   * @return 承Jgみであればtrue、そうでなければfalse
   */
  function hasConfirmed(bytes32 type_, address addr, uint proposalId) internal view returns (bool) {
    Proposal storage proposal = proposals[type_][proposalId];
    uint length = proposal.confirmers.length;

    for (uint i = 0; i < length; i++) {
      if (proposal.confirmers[i] == addr) return true;
    }

    return false;
  }

  /**
   * 指定した提案を承Jしたト`クンのt量を返す
   *
   * @param type_ 提案のN
   * @param proposalId 提案ID
   *
   * @return 承Jに投票されたト`クン数
   */
  function confirmedTotalToken(bytes32 type_, uint proposalId) internal view returns (uint) {
    Proposal storage proposal = proposals[type_][proposalId];
    uint length = proposal.confirmers.length;
    uint total = 0;

    for (uint i = 0; i < length; i++) {
      total = add(total, _storage.tokenBalance(proposal.confirmers[i]));
    }

    return total;
  }

  /**
   * 指定した提案のめ切りを返す
   *
   * @param type_ 提案のN
   * @param proposalId 提案ID
   *
   * @return 提案のめ切り
   */
  function proposalExpireAt(bytes32 type_, uint proposalId) public view returns (uint) {
    Proposal storage proposal = proposals[type_][proposalId];
    return proposal.expireAt;
  }

  /**
   * 提案がg行条件を氦郡筏皮い毪を返す
   *
   * 【承J条件】
   * - まだg行していない
   * - 提案の有科谙弈冥扦る
   * - 指定した割合以上のm成ト`クンを得ている
   *
   * @param proposalId 提案ID
   *
   * @return g行条件を氦郡筏皮い龊悉true、そうでない龊悉false
   */
  function isProposalExecutable(bytes32 type_, uint proposalId, address proposer, bytes transactionBytecode) internal view returns (bool) {
    Proposal storage proposal = proposals[type_][proposalId];

    // オ`ナ`がcontrollerを登hしたユ`ザ`しか存在しない龊悉
    if (_storage.numOwnerAddress() < 2) {
      return true;
    }

    return  proposal.digest == keccak256(proposer, proposal.amount, transactionBytecode) &&
            isProposalNotExpired(type_, proposalId) &&
            mul(100, confirmedTotalToken(type_, proposalId)) / _storage.totalSupply() > confirmTotalTokenThreshold;
  }

  /**
   * 指定したNの提案数を取得する
   *
   * @param type_ 提案のN（'mint' | 'burn' | 'transferMinimumFee' | 'transferFeeRate'）
   *
   * @return 提案数（承Jされていないものも含む）
   */
  function numberOfProposals(bytes32 type_) public constant returns (uint) {
    return proposals[type_].length;
  }

  /**
   * 未承Jで有科谙蓼吻肖欷皮い胜ぬ岚袱问を返す
   *
   * @param type_ 提案のN（'mint' | 'burn' | 'transferMinimumFee' | 'transferFeeRate'）
   *
   * @return 提案数
   */
  function numberOfActiveProposals(bytes32 type_) public view returns (uint) {
    uint numActiveProposal = 0;

    for(uint i = 0; i < proposals[type_].length; i++) {
      if (isProposalNotExpired(type_, i)) {
        numActiveProposal++;
      }
    }

    return numActiveProposal;
  }

  /**
   * 提案の有科谙蓼切れていないかチェックする
   *
   * - g行されていない
   * - 有科谙蓼切れていない
   *
   * 龊悉韦trueを返す
   */
  function isProposalNotExpired(bytes32 type_, uint proposalId) internal view returns (bool) {
    Proposal storage proposal = proposals[type_][proposalId];

    return  !proposal.executed &&
            now < proposal.expireAt;
  }

  /**
   * dJPYを造する
   *
   * - 造する量が0より大きい
   *
   * 龊悉铣晒Δ工
   *
   * @param proposalId 提案ID
   */
  function executeMintProposal(uint proposalId) internal returns (bool) {
    Proposal storage proposal = proposals[keccak256('mint')][proposalId];

    // ここでも念のためチェックを入れる
    require(proposal.amount > 0);

    emit MintExecuted(proposalId, proposal.proposer, proposal.amount);

    // t供o量 / g行者のdJPY / g行者のSHINJI tokenをやす
    _storage.addTotalSupply(proposal.amount);
    _storage.addCoinBalance(proposal.proposer, proposal.amount);
    _storage.addTokenBalance(proposal.proposer, proposal.amount);

    return true;
  }

  /**
   * dJPYを消却する
   *
   * - 消却する量が0より大きい
   * - 提案者の所有するdJPYの残高がamount以上
   * - 提案者の所有するSHINJIがamountよりも大きい
   *
   * 龊悉铣晒Δ工
   *
   * @param proposalId 提案ID
   */
  function executeBurnProposal(uint proposalId) internal returns (bool) {
    Proposal storage proposal = proposals[keccak256('burn')][proposalId];

    // ここでも念のためチェックを入れる
    require(proposal.amount > 0);
    require(_storage.coinBalance(proposal.proposer) >= proposal.amount);
    require(_storage.tokenBalance(proposal.proposer) >= proposal.amount);

    emit BurnExecuted(proposalId, proposal.proposer, proposal.amount);

    // t供o量 / g行者のdJPY / g行者のSHINJI tokenをpらす
    _storage.subTotalSupply(proposal.amount);
    _storage.subCoinBalance(proposal.proposer, proposal.amount);
    _storage.subTokenBalance(proposal.proposer, proposal.amount);

    return true;
  }

  function isAuthorized(address src, bytes4 sig) internal view returns (bool) {
    sig; // #HACK

    return  src == address(this) ||
            src == owner ||
            src == address(_token);
  }
}